import (
	"std.um"

	"common.um"
	"chunk.um"
	"debug.um"
	"scanner.um"
	"value.um"
)

type Parser = struct {
	current, previous: scanner::Token
	hadError, panicMode: bool
}

type Precedence = enum {
	None = 0
	Assignment = 1
	Or = 2
	And = 3
	Equality = 4
	Comparison = 5
	Term = 6
	Factor = 7
	Unary = 8
	Call = 9
	Primary = 10
}

type ParseFn = fn(canAssign: bool)

type ParseRule = struct {
	prefix, infix: ParseFn
	precedence: Precedence
}

type Local = struct {
	name: scanner::Token
	depth: uint
}

type Compiler = struct {
	locals: [common::UINT8_COUNT]Local
	localCount, scopeDepth: int
}

var parser: Parser
var current: ^Compiler = null
var compilingChunk: ^chunk::Chunk

fn currentChunk(): ^chunk::Chunk { return compilingChunk }

fn errorAt(token: ^scanner::Token, message: str) {
	if parser.panicMode { return }
	parser.panicMode = true

	fprintf(std::stderr(), "[line %d] Error", token.line)

	if token.type_ == .Eof {
		fprintf(std::stderr(), " at end")
	} else if token.type_ != .Error {
		fprintf(std::stderr(), " at '%s'", token.lexeme)
	}

	fprintf(std::stderr(), ": %s\n", message)
	parser.hadError = true
}

fn error(message: str) {
	errorAt(&parser.previous, message)
}

fn errorAtCurrent(message: str) {
	errorAt(&parser.current, message)
}

fn advance() {
	parser.previous = parser.current

	for true {
		parser.current = scanner::scanToken()
		if parser.current.type_ != .Error { break }

		errorAtCurrent(parser.current.lexeme)
	}
}

fn consume(type_: scanner::TokenType, message: str) {
	if parser.current.type_ == type_ {
		advance()
		return
	}

	errorAtCurrent(message)
}

fn check(type_: scanner::TokenType): bool {
	return parser.current.type_ == type_
}

fn match(type_: scanner::TokenType): bool {
	if !check(type_) { return false }
	advance()
	return true
}

fn emitByte(byte: uint8) {
	currentChunk().writeByte(byte, parser.previous.line)
}

fn emitOpCode(op: chunk::OpCode) {
	currentChunk().writeOpCode(op, parser.previous.line)
}

fn emitOpAndByte(op: chunk::OpCode, byte: uint8) {
	emitOpCode(op)
	emitByte(byte)
}

fn emitOpCodes(op1: chunk::OpCode, op2: chunk::OpCode) {
	emitOpCode(op1)
	emitOpCode(op2)
}

fn emitLoop(start: uint) {
	emitOpCode(.Loop)

	offset := currentChunk().length() - start + 2
	if offset > common::UINT16_MAX { error("Loop body too large.") }

	emitByte((offset >> 8) & 0xff)
	emitByte(offset & 0xff)
}

fn emitJump(instruction: chunk::OpCode): uint {
	emitOpCode(instruction)
	emitByte(0xff)
	emitByte(0xff)
	return currentChunk().length() - 2
}

fn emitReturn() {
	emitOpCode(.Return)
}

fn makeConstant(v: value::Value): uint8 {
	constant := currentChunk().addConstant(v)

	if constant > common::UINT8_MAX {
		error("Too many constants in one chunk.")
		return 0
	}

	return uint8(constant)
}

fn emitConstant(v: value::Value) {
	emitOpAndByte(.Constant, makeConstant(v))
}

fn patchJump(offset: uint) {
	jump := currentChunk().length() - offset - 2

	if jump > common::UINT16_MAX {
		error("Too much code to jump over.")
	}

	currentChunk().code[offset] = (jump >> 8) & 0xff
	currentChunk().code[offset + 1] = jump & 0xff
}

fn (compiler: ^Compiler) init() {
	compiler.localCount = 0
	compiler.scopeDepth = 0
	current = compiler
}

fn endCompiler() {
	emitReturn()
	if common::DEBUG_PRINT_CODE && !parser.hadError {
		debug::disassembleChunk(currentChunk(), "code")
	}
}

fn (c: ^Compiler) beginScope() { c.scopeDepth++ }

fn (c: ^Compiler) endScope() {
	c.scopeDepth--

	// TODO: maybe optimize later
	for c.localCount > 0 &&
	    c.locals[c.localCount - 1].depth > c.scopeDepth {
		emitOpCode(.Pop)
		c.localCount--
	}
}

fn expression(); fn statement(); fn declaration()
fn getRule(type_: scanner::TokenType): ^ParseRule
fn parsePrecedence(precedence: Precedence)

fn identifierConstant(name: ^scanner::Token): uint8 {
	return makeConstant(value::String(name.lexeme))
}

fn identifiersEqual(a, b: ^scanner::Token): bool {
	// yeah basically
	return a == b || a.lexeme == b.lexeme
}

fn (c: ^Compiler) resolveLocal(name: ^scanner::Token): int {
	for i := c.localCount - 1; i >= 0; i-- {
		local := &c.locals[i]
		if identifiersEqual(name, &local.name) {
			if local.depth == -1 {
				error("Can't read local variable in its own initializer.")
			}
			return i
		}
	}
	return -1
}

fn (c: ^Compiler) addLocal(name: scanner::Token) {
	if c.localCount == common::UINT8_COUNT {
		error("Too many local variables in one function.")
		return
	}

	local := &c.locals[c.localCount]
	c.localCount++
	local.name = name
	local.depth = -1
}

fn declareVariable() {
	if current.scopeDepth == 0 { return }

	name := &parser.previous
	for i := current.localCount - 1; i >= 0; i-- {
		local := &current.locals[i]
		if local.depth != -1 && local.depth < current.scopeDepth {
			break
		}

		if identifiersEqual(name, &local.name) {
			error("Already a variable with this name in this scope.")
		}
	}

	current.addLocal(name^)
}

fn parseVariable(errorMessage: str): uint8 {
	consume(.Identifier, errorMessage)

	declareVariable()
	if current.scopeDepth > 0 { return 0 }

	return identifierConstant(&parser.previous)
}

// mark latest declared local as defined (initialized)
fn markInitialized() {
	current.locals[current.localCount-1].depth =
		current.scopeDepth
}

fn defineVariable(global: uint8) {
	if current.scopeDepth > 0 {
		markInitialized()
		return
	}

	emitOpAndByte(.DefineGlobal, global)
}

fn and(_: bool) {
	endJump := emitJump(.JumpIfFalse)
	emitOpCode(.Pop)
	parsePrecedence(.And)
	patchJump(endJump)
}

fn binary(_: bool) {
	operatorType := parser.previous.type_
	rule := getRule(operatorType)
	parsePrecedence(Precedence(uint(rule.precedence) + 1))

	switch operatorType {
	case .BangEqual: emitOpCodes(.Equal, .Not)
	case .EqualEqual: emitOpCode(.Equal)
	case .Greater: emitOpCode(.Greater)
	case .GreaterEqual: emitOpCodes(.Less, .Not)
	case .Less: emitOpCode(.Less)
	case .LessEqual: emitOpCodes(.Greater, .Not)

	case .Plus:  emitOpCode(.Add)
	case .Minus: emitOpCode(.Subtract)
	case .Star:  emitOpCode(.Multiply)
	case .Slash: emitOpCode(.Divide)
	}
}

fn literal(_: bool) {
	switch parser.previous.type_ {
	case .False: emitOpCode(.False)
	case .Nil: emitOpCode(.Nil)
	case .True: emitOpCode(.True)
	}
}

fn grouping(_: bool) {
	expression()
	consume(.RightParen, "Expect ')' after expression.")
}

fn number(_: bool) {
	v := std::atof(parser.previous.lexeme)
	emitConstant(value::Number(v))
}

fn or(_: bool) {
	elseJump := emitJump(.JumpIfFalse)
	endJump := emitJump(.Jump)

	patchJump(elseJump)
	emitOpCode(.Pop)

	parsePrecedence(.Or)
	patchJump(endJump)
}

fn string(_: bool) {
	emitConstant(value::String(
		slice(parser.previous.lexeme, 1, len(parser.previous.lexeme)-1)))
}

fn namedVariable(name: scanner::Token, canAssign: bool) {
	var getOp, setOp: chunk::OpCode
	arg := current.resolveLocal(&name)
	if (arg != -1) {
		getOp = .GetLocal
		setOp = .SetLocal
	} else {
		arg = identifierConstant(&name)
		getOp = .GetGlobal
		setOp = .SetGlobal
	}

	if canAssign && match(.Equal) {
		expression()
		emitOpAndByte(setOp, uint8(arg))
	} else {
		emitOpAndByte(getOp, uint8(arg))
	}
}

fn variable(canAssign: bool) {
	namedVariable(parser.previous, canAssign)
}

fn unary(_: bool) {
	operatorType := parser.previous.type_

	expression()

	switch operatorType {
	case .Minus: emitOpCode(.Negate)
	case .Bang: emitOpCode(.Not)
	}
}

var initedRules: bool = false
var rules: [uint(scanner::TokenType.Eof)+1]ParseRule
var hitUndef: bool = false
fn undef(_: bool) { error("Expect expression."); hitUndef = true }

fn parsePrecedence(precedence: Precedence) {
	advance()
	canAssign := precedence <= .Assignment
	getRule(parser.previous.type_).prefix(canAssign)
	if hitUndef { return }
	hitUndef = false

	for precedence <= getRule(parser.current.type_).precedence {
		advance()
		getRule(parser.previous.type_).infix(canAssign)
		if hitUndef { std::assert(false) }
	}

	if canAssign && match(.Equal) {
		error("Invalid assignment target.")
	}
}

fn getRule(type_: scanner::TokenType): ^ParseRule {
	return &rules[uint(type_)]
}

fn expression() {
	parsePrecedence(.Assignment)
}

fn block() {
	for !check(.RightBrace) && !check(.Eof) { declaration() }
	consume(.RightBrace, "Expect '}' after block.")
}

fn varDeclaration() {
	global := parseVariable("Expect variable name.")

	if match(.Equal) {
		expression()
	} else {
		emitOpCode(.Nil)
	}

	consume(.Semicolon, "Expect ';' after variable declaration.")
	defineVariable(global)
}

fn expressionStatement() {
	expression()
	consume(.Semicolon, "Expect ';' after expression.")
	emitOpCode(.Pop)
}

fn forStatement() {
	current.beginScope()
	consume(.LeftParen, "Expect '(' after 'for'.")

	if match(.Semicolon) {
		// No initializer.
	} else if match(.Var) {
		varDeclaration();
	} else {
		expressionStatement();
	}

	loopStart := currentChunk().length()
	exitJump := -1

	if !match(.Semicolon) {
		expression()
		consume(.Semicolon, "Expect ';' after loop condition.")

		exitJump = emitJump(.JumpIfFalse)
		emitOpCode(.Pop)
	}

	if !match(.RightParen) {
		bodyJump := emitJump(.Jump)
		incrementStart := currentChunk().length()
		expression()
		emitOpCode(.Pop)
		consume(.RightParen, "Expect ')' after for clauses.")

		emitLoop(loopStart)
		loopStart = incrementStart
		patchJump(bodyJump)
	}

	statement()
	emitLoop(loopStart)

	if exitJump != -1 {
		patchJump(exitJump)
		emitOpCode(.Pop)
	}

	current.endScope()
}

fn ifStatement() {
	consume(.LeftParen, "Expect '(' after 'if'.")
	expression()
	consume(.RightParen, "Expect ')' after condition.")

	thenJump := emitJump(.JumpIfFalse)
	emitOpCode(.Pop)
	statement()

	elseJump := emitJump(.Jump)

	patchJump(thenJump)
	emitOpCode(.Pop)

	if match(.Else) { statement() }
	patchJump(elseJump)
}

fn printStatement() {
	expression()
	consume(.Semicolon, "Expect ';' after value.")
	emitOpCode(.Print)
}

fn whileStatement() {
	loopStart := currentChunk().length()
	consume(.LeftParen, "Expect '(' after 'while'.")
	expression()
	consume(.RightParen, "Expect ')' after condition.")

	exitJump := emitJump(.JumpIfFalse)
	emitOpCode(.Pop)
	statement()
	emitLoop(loopStart)

	patchJump(exitJump)
	emitOpCode(.Pop)
}

fn synchronize() {
	parser.panicMode = false

	for parser.current.type_ != .Eof {
		if parser.previous.type_ == .Semicolon { return }
		switch parser.current.type_ {
		case .Class, .Fun, .Var, .For,
		     .If, .While, .Print, .Return:
			return
		}
		advance()
	}
}

fn statement() {
	if match(.Print) {
		printStatement()
	} else if match(.For) {
		forStatement()
	} else if match(.If) {
		ifStatement()
	} else if match(.While) {
		whileStatement()
	} else if match(.LeftBrace) {
		current.beginScope()
		block()
		current.endScope()
	} else {
		expressionStatement()
	}
}

fn declaration() {
	if match(.Var) {
		varDeclaration()
	} else {
		statement()
	}

	if parser.panicMode { synchronize() }
}

fn initRules() {
	for _, r^ in rules {
		r.prefix = undef
		r.infix = undef
		r.precedence = .None
	}

	rules[uint(scanner::TokenType.LeftParen)].prefix = grouping
	rules[uint(scanner::TokenType.Minus)] = {unary, binary, .Term}
	rules[uint(scanner::TokenType.Plus)] = {undef, binary, .Term}
	rules[uint(scanner::TokenType.Slash)] = {undef, binary, .Factor}
	rules[uint(scanner::TokenType.Star)] = {undef, binary, .Factor}
	rules[uint(scanner::TokenType.Number)].prefix = number
	rules[uint(scanner::TokenType.Nil)].prefix = literal
	rules[uint(scanner::TokenType.True)].prefix = literal
	rules[uint(scanner::TokenType.False)].prefix = literal
	rules[uint(scanner::TokenType.Bang)].prefix = unary
	rules[uint(scanner::TokenType.BangEqual)] = {undef, binary, .Equality}
	rules[uint(scanner::TokenType.EqualEqual)] = {undef, binary, .Equality}
	rules[uint(scanner::TokenType.Greater)] = {undef, binary, .Comparison}
	rules[uint(scanner::TokenType.GreaterEqual)] = {undef, binary, .Comparison}
	rules[uint(scanner::TokenType.Less)] = {undef, binary, .Comparison}
	rules[uint(scanner::TokenType.LessEqual)] = {undef, binary, .Comparison}
	rules[uint(scanner::TokenType.String)] = {string, undef, .None}
	rules[uint(scanner::TokenType.Identifier)].prefix = variable
	rules[uint(scanner::TokenType.And)] = {undef, and, .And}
	rules[uint(scanner::TokenType.Or)] = {undef, or, .Or}

	initedRules = true
}

fn compile*(source: str, c: ^chunk::Chunk): bool {
	if !initedRules {
		initRules()
		initedRules = true
	}

	scanner::init(source)
	var compiler: Compiler
	compiler.init()
	compilingChunk = c

	parser.hadError = false
	parser.panicMode = false

	advance()

	for !match(.Eof) { declaration() }

	endCompiler()

	compilingChunk = null
	current = null
	return !parser.hadError
}
