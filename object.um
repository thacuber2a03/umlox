import (
	"std.um"

	"chunk.um"
	"common.um"
	"value.um"
)

var allObjects: []value::Value

type Function* = struct {
	arity, upvalueCount: uint
	chunk: chunk::Chunk
	name: value::String
}

fn (f: ^Function) print*() {
	if f.name == "" {
		printf("<script>")
		return
	}

	printf("<fn %s>", f.name)
}

fn (f: ^Function) isFalsey*(): bool { return false }
fn (f: ^Function) unlink*() { f.chunk.unlink() }
fn (f: ^Function) equals*(v: ^value::Value): bool {
	o := ^Function(v^); return f == o
}

fn newFunction*(): ^Function {
	f := new(Function)
	f.chunk.init()
	allObjects = append(allObjects, f)
	return f
}

// TODO: expose the stack in some other way but this
type NativeFunction* = fn(argCount: uint, stack: []value::Value, base: uint): value::Value

fn (f: ^NativeFunction) print*() { printf("<native fn>") }
fn (f: ^NativeFunction) isFalsey*(): bool { return false }
fn (f: ^NativeFunction) unlink*() {}

fn (f: ^NativeFunction) equals*(v: ^value::Value): bool {
	o := ^NativeFunction(v^); return f == o
}

type Upvalue* = struct {
	location: ^value::Value
	slot: uint
	closed: value::Value
	next: ^Upvalue
}

fn (u: ^Upvalue) print*() { std::assert(false) }
fn (u: ^Upvalue) isFalsey*(): bool { return false }

fn (u: ^Upvalue) equals*(v: ^value::Value): bool {
	return v.equals(u.location)
}

fn (u: ^Upvalue) unlink*() {
	if u.location != null && valid(u.location^) {
		v := u.location^
		u.location = null
		v.unlink()
	}

	if valid(u.closed) { u.closed.unlink() }

	if u.next != null {
		u.next.unlink()
		u.next = null
	}
}

fn newUpvalue*(stack: []value::Value, slot: uint): ^Upvalue {
	u := new(Upvalue)
	u.location = &stack[slot]
	u.slot = slot
	u.closed = value::Nil {}
	u.next = null
	allObjects = append(allObjects, u)
	return u
}

type Closure* = struct {
	function: ^Function
	upvalues: []^Upvalue
}

fn (c: ^Closure) print*() { c.function.print() }
fn (c: ^Closure) isFalsey*(): bool { return false }

fn (c: ^Closure) equals*(v: ^value::Value): bool {
	// TODO: should I compare the function pointers
	// or the closure pointers? currently defaulting
	// to functions
	return c.function.equals(v)
}

fn (c: ^Closure) unlink*() {
	c.function = null
	for true {
		if len(c.upvalues) == 0 { break }
		v := c.upvalues[len(c.upvalues)-1]
		delete(c.upvalues, len(c.upvalues)-1)
		v.unlink()
	}
}

fn newClosure*(function: ^Function): ^Closure {
	c := new(Closure)
	c.function = function
	c.upvalues = make([]^Upvalue, function.upvalueCount)
	allObjects = append(allObjects, c)
	return c
}

fn unlinkAllObjects*() {
	for true {
		if len(allObjects) == 0 { break }
		v := allObjects[len(allObjects)-1]
		delete(allObjects, len(allObjects)-1)
		v.unlink()
	}
}
